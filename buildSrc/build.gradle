plugins {
    id 'com.google.protobuf' version '0.8.16'
    id 'signing'
    id 'maven-publish'
}

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

apply from: 'misq-version.gradle'

ext {
    createTestReports = false
}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

java {
    modularity.inferModulePath = false
    withJavadocJar()
    withSourcesJar()
}

tasks.named('compileJava') {
    sourceCompatibility = 16
    targetCompatibility = 16
    modularity.inferModulePath = false
    options.compilerArgs += ['-Xdoclint:none', '-Xlint:none', '-nowarn']
}

jar {
    // Exclude .proto files from generated jars.
    exclude '*.proto'
    includeEmptyDirs false
}

tasks.withType(Test).configureEach {
    maxParallelForks = Runtime.runtime.availableProcessors().intdiv(2) ?: 1
    if (!project.hasProperty("createTestReports")) {
        reports.html.enabled = false
        reports.junitXml.enabled = false
    }
}

tasks.named("javadoc") {
    source = sourceSets.main.allJava
    options.addStringOption('Xdoclint:none', '-quiet')
}

tasks.named("test") {
    useJUnitPlatform()
    exclude '**/**Integration*'
}
