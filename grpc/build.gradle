plugins {
    id 'java-library'
    id 'com.google.protobuf' version '0.8.16'
}

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

apply from: '../buildSrc/misq-version.gradle'
apply from: '../buildSrc/logging-dependencies.gradle'
apply from: '../buildSrc/lombok-dependencies.gradle'
apply from: '../buildSrc/test-dependencies.gradle'

dependencies {
    api platform(project(':platforms:grpc-platform'))

    implementation project(':api')
    implementation project(':application')
    implementation 'javax.annotation:javax.annotation-api'
    implementation 'com.google.protobuf:protobuf-java'
    implementation 'io.grpc:grpc-api'
    implementation 'io.grpc:grpc-core'
    implementation 'io.grpc:grpc-context'
    implementation 'io.grpc:grpc-netty-shaded'
    implementation 'io.grpc:grpc-protobuf'
    implementation 'io.grpc:grpc-services'
    implementation 'io.grpc:grpc-stub'
    implementation 'org.codehaus.mojo:animal-sniffer-annotations'
    implementation 'com.google.guava:guava'
}

ext {
    // Default generated src dirs.
    generatedGrpcProtoSrcDir = 'build/generated/source/main/grpc'
    generatedProtoSrcDir = 'build/generated/source/main/java'
}

sourceSets.main {
    java.srcDirs += [generatedProtoSrcDir, generatedGrpcProtoSrcDir]
}

protobuf {
    protoc {
        // Append 'osxArch' property to workaround M1 Mac bug.
        artifact = "com.google.protobuf:protoc:3.12.4${osxArch}"
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:1.38.0${osxArch}"
        }
    }
    generateProtoTasks {
        all()*.plugins { grpc {} }
    }
    generatedFilesBaseDir = "$projectDir/build/generated/source"
}

test {
    useJUnitPlatform()
    exclude '**/**Integration*'
}
